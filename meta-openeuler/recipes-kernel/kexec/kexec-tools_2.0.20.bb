SUMMARY = "Kexec fast reboot tools"
DESCRIPTION = "Kexec is a fast reboot feature that lets you reboot to a new Linux kernel"
AUTHOR = "Eric Biederman"
HOMEPAGE = "http://kernel.org/pub/linux/utils/kernel/kexec/"
SECTION = "kernel/userland"
LICENSE = "GPLv2"
LIC_FILES_CHKSUM = "file://COPYING;md5=ea5bed2f60d357618ca161ad539f7c0a \
                    file://kexec/kexec.c;beginline=1;endline=20;md5=af10f6ae4a8715965e648aa687ad3e09"
DEPENDS = "zlib"

SRC_URI = "file://kexec-tools/kexec-tools-${PV}.tar.xz \
           file://kexec-tools/kexec-tools-2.0.20-fix-broken-multiboot2-buliding-for-i386.patch \
           file://kexec-tools/kexec-tools-2.0.20-Remove-duplicated-variable-declarations.patch \
           file://kexec-tools/kexec-add-variant-helper-functions-for-handling-memory-regions.patch \
           file://kexec-tools/arm64-kexec-allocate-memory-space-avoiding-reserved-regions.patch \
           file://kexec-tools/x86-Fix-PAGE_OFFSET-for-kernels-since-4.20.patch \
           file://kexec-tools/Cleanup-remove-the-read_elf_kcore.patch \
           file://kexec-tools/Fix-an-error-definition-about-the-variable-fname.patch \
           file://kexec-tools/Cleanup-move-it-back-from-util_lib-elf_info.c.patch \
           file://kexec-tools/Limit-the-size-of-vmcore-dmesg.txt-to-2G.patch \
           file://kexec-tools/vmcore-dmesg-vmcore-dmesg.c-Fix-shifting-error-reported-by-cppcheck.patch \
           file://kexec-tools/kexec-tools-Fix-possible-out-of-bounds-access-in-ifdown.patch \
           file://kexec-tools/kexec-tools-Fix-kexec_file_load-2-error-handling.patch \
           file://kexec-tools/kexec-tools-Reset-getopt-before-falling-back-to-legacy.patch \ 
           file://kexec-tools/kexec-support-parsing-the-string-Reserved-to-get-the-correct-e820-reserved-region.patch \
           file://kexec-tools/arm64-kdump-deal-with-a-lot-of-resource-entries-in-proc-iomem.patch \
"
SRC_URI_append_aarch64 += " \
           file://kexec-tools/arm64-support-more-than-one-crash-kernel-regions.patch \
"
SRC_URI_append += " \
           file://kexec-tools/kexec-Add-quick-kexec-support.patch \
"
SRC_URI_append_aarch64 += " \
           file://kexec-tools/kexec-Quick-kexec-implementation-for-arm64.patch \
"
SRC_URI_append += " \
          file://kexec-tools/backport-print-add-support-for-lockless-ringbuffer.patch \
          file://kdump \
          file://kdump.conf \
          file://kdump.service \
"
SRC_URI[sha256sum] = "dad8077f0315445d1f6335579fc4ade222facf82a67124974c7be5303ba4f8c8"

inherit autotools

export LDFLAGS = "-L${STAGING_LIBDIR}"
EXTRA_OECONF = " --with-zlib=yes"

do_compile_prepend() {
    # Remove the prepackaged config.h from the source tree as it overrides
    # the same file generated by configure and placed in the build tree
    rm -f ${S}/include/config.h

    # Remove the '*.d' file to make sure the recompile is OK
    for dep in `find ${B} -type f -name '*.d'`; do
        dep_no_d="`echo $dep | sed 's#.d$##'`"
        # Remove file.d when there is a file.o
        if [ -f "$dep_no_d.o" ]; then
            rm -f $dep
        fi
    done
}

do_install_append () {
        install -d ${D}${sysconfdir}/sysconfig
        install -m 0644 ${WORKDIR}/kdump.conf ${D}${sysconfdir}/sysconfig

        if ${@bb.utils.contains('DISTRO_FEATURES', 'sysvinit', 'true', 'false', d)}; then
                install -D -m 0755 ${WORKDIR}/kdump ${D}${sysconfdir}/init.d/kdump
        fi

        if ${@bb.utils.contains('DISTRO_FEATURES', 'systemd', 'true', 'false', d)}; then
                install -D -m 0755 ${WORKDIR}/kdump ${D}${libexecdir}/kdump-helper
                install -D -m 0644 ${WORKDIR}/kdump.service ${D}${systemd_unitdir}/system/kdump.service
                sed -i -e 's,@LIBEXECDIR@,${libexecdir},g' ${D}${systemd_unitdir}/system/kdump.service
        fi
}

PACKAGES =+ "kexec kdump vmcore-dmesg"

ALLOW_EMPTY_${PN} = "1"
RRECOMMENDS_${PN} = "kexec kdump vmcore-dmesg"

FILES_kexec = "${sbindir}/kexec"
FILES_kdump = "${sbindir}/kdump \
               ${sysconfdir}/sysconfig/kdump.conf \
               ${sysconfdir}/init.d/kdump \
               ${libexecdir}/kdump-helper \
               ${systemd_unitdir}/system/kdump.service \
"

FILES_vmcore-dmesg = "${sbindir}/vmcore-dmesg"

INITSCRIPT_PACKAGES = "kdump"
INITSCRIPT_NAME_kdump = "kdump"
INITSCRIPT_PARAMS_kdump = "start 56 2 3 4 5 . stop 56 0 1 6 ."

SYSTEMD_PACKAGES = "kdump"
SYSTEMD_SERVICE_kdump = "kdump.service"

SECURITY_PIE_CFLAGS_remove = "-fPIE -pie"

COMPATIBLE_HOST = '(x86_64.*|i.86.*|arm.*|aarch64.*|powerpc.*|mips.*)-(linux|freebsd.*)'

INSANE_SKIP_${PN} = "arch"
