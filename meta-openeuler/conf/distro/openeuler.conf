DISTRO = "openeuler"
DISTRO_NAME = "openEuler embedded(openEuler Embedded Reference Distro)"
DISTRO_VERSION = "22.03"
DISTRO_CODENAME = "openEuler${@d.getVar('DISTRO_VERSION').replace('.', '_')}"
SDK_VENDOR = "-openeulersdk"
SDK_VERSION = "${@d.getVar('DISTRO_VERSION').replace('snapshot-${METADATA_REVISION}', 'snapshot')}"
SDK_VERSION[vardepvalue] = "${SDK_VERSION}"

MAINTAINER = "openEuler <dev@openeuler.org>"

TARGET_VENDOR = "-openeuler"

LOCALCONF_VERSION = "2"

# use busybox as default init manager and device manager
INIT_MANAGER ?= "mdev-busybox"
# VIRTUAL-RUNTIME_dev_manager in packagegroup-core-boot.bb has higher
# priority than the one in init-manager-mdev-busybox.inc, so here
# use OPENEULER_DEV_MANAGER as a workaround
OPENEULER_DEV_MANAGER ?= "busybox-mdev"
VIRTUAL-RUNTIME_dev_manager = "${OPENEULER_DEV_MANAGER}"

# Override these in openeuler embedded based distros

# pam package is used for authentication
OPENEULER_DEFAULT_DISTRO_FEATURES = "pam"

DISTRO_FEATURES ?= "${DISTRO_FEATURES_DEFAULT} ${OPENEULER_DEFAULT_DISTRO_FEATURES}"

PREFERRED_VERSION_linux-openeuler ?= "5.10%"

#SDK related configurations
SDK_NAME = "${DISTRO}-${TCLIBC}-${SDKMACHINE}-${IMAGE_BASENAME}-${TUNE_PKGARCH}-${MACHINE}"
# set default sdk install directory
SDKPATH = "/opt/${DISTRO}/${SDK_VERSION}"

DISTRO_EXTRA_RDEPENDS += "${OPENEULER_DEFAULT_EXTRA_RDEPENDS}"
DISTRO_EXTRA_RRECOMMENDS += "${OPENEULER_DEFAULT_EXTRA_RRECOMMENDS}"

TCLIBCAPPEND = ""
TCLIBC = "glibc"

# do not use default download dir, use openeuler's configuration
DL_DIR ?= "${OPENEULER_SP_DIR}/${BPN}"

# the following be configured in local.conf or global bbclass
OPENEULER_SP_BRANCH ?= "openEuler-${DISTRO_VERSION}-LTS"
OPENEULER_SP_REPO = "git://gitee.com/src-openeuler/${BPN};branch=${OPENEULER_SP_BRANCH};protocol=https"

# use openeuler software package repo as pre mirror, yocto will fetch *.tar.gz from pre mirror first, then
# go to SRC_URI
PREMIRRORS ??= "\
http://.*/.*  ${OPENEULER_SP_REPO} \n \
https://.*/.* ${OPENEULER_SP_REPO} \n \
bzr://.*/.*   ${OPENEULER_SP_REPO} \n \
cvs://.*/.*   ${OPENEULER_SP_REPO} \n \
git://.*/.*   ${OPENEULER_SP_REPO} \n \
gitsm://.*/.* ${OPENEULER_SP_REPO} \n \
hg://.*/.*    ${OPENEULER_SP_REPO} \n \
osc://.*/.*   ${OPENEULER_SP_REPO} \n \
p4://.*/.*    ${OPENEULER_SP_REPO} \n \
svn://.*/.*   ${OPENEULER_SP_REPO} \n \"

# QA check settings - a little stricter than the OE-Core defaults
# (none currently necessary as we now match OE-Core)
#WARN_TO_ERROR_QA = "X"
#WARN_QA_remove = "${WARN_TO_ERROR_QA}"
#ERROR_QA_append = " ${WARN_TO_ERROR_QA}"

# for embedded case, the mechanism of update-alternatives is not
# enabled to only allow just one copy of binary, to save memory
# footprint. Therefore, if multiple packages provide the same file,
# conflict error will be reported
INHERIT_remove += "update-alternatives"
# As update-alternatives is disabled, no need to do qa check of
# missing-update-alternatives
WARN_QA_remove += "missing-update-alternatives"


INHERIT += "reproducible_build"

BB_SIGNATURE_HANDLER ?= "OEEquivHash"
BB_HASHSERVE ??= "auto"


PREFERRED_PROVIDER_virtual/kernel = "linux-openeuler"
#add by lisimin,delete depends
#UNINATIVE_CHECKSUM[x86_64] = "dead"
#UNINATIVE_URL = "unset"

INHIBIT_PACKAGE_DEBUG_SPLIT = "1"

# for all .bb file, OPENEULER_SP_DIR  will be included
# in file path
FILESEXTRAPATHS_prepend = "${OPENEULER_SP_DIR}:"

# the sysroot of openeuler native sdk
OPENEULER_NATIVESDK_SYSROOT ?= ""


# PACKAGE_NO_LOCALE is variable in package.bbclass to control
# whether do_split_locales() for pacakges.
# openEuler Embedded is used for scenarios with limit memory, aslo
# in current image, no locale related pacakges are included.
# so we use variable to disable the generation of locale related packages
# it will save some build time, also no extra locale-* packages
PACKAGE_NO_LOCALE ?= "1"

# as host rpm is used, so need to get the correct host rpmdeps by get_rpmdeps
export RPMDEPS = "default"
INHERIT += "openeuler"

#disable the file checksum of fetch
do_fetch[file-checksums] = ""
SDK_INCLUDE_BUILDTOOLS = "0"
require conf/distro/openeuler-bsp.conf
